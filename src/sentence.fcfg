% start ROOT

# ##########
# GRAMMMAR PRODUCTIONS
# ##########

#TOP-LEVEL
ROOT[TYPE=sent] -> S[-INTERROG, POL=?p] FS 
ROOT[TYPE=ynQ] -> S[+INTERROG, POL=?p] QM

#CONSTITUENT QUESTIONS WITH RELATIVE CLAUSES
ROOT[TYPE=relQ] -> ARG[TYPE=ciamar] REL/ARG QM
ROOT[TYPE=relQ] -> ARG[TYPE=co] REL/ARG QM
ROOT[TYPE=relQ] -> ARG[TYPE=de] REL/ARG QM

#CONSTITUENT QUESTIONS WITH SENTENCE
ROOT[TYPE=nonrelQ] -> ARG[TYPE=cait] S[+INTERROG, POL=?p]/ARG[TYPE=cait] QM

#RELATIVE PHRASE
#Ciamar a chaidh thu ann? Ciamar / a chaidh thu ann?
REL[POL=?p]/?x -> AREL[POL=?p] S[INTERROG=?i, POL=?p]/?x

#SLASHED SENTENCE FOR SUBSTANTIVE VERB WITH 1 ARGUMENT
S[INTERROG=?i, POL=?p]/?x -> VI[INTERROG=?i, POL=?p, SUBCAT=?s] NP[CASE=rad] ARG/ARG

#UNIVERSAL SLASHED ARGUMENT
ARG/ARG ->

FS -> "." | "!"
QM -> "?"

#[TOWRITE] these are at ROOT level, AN is at V level, hence in verb file
ARG[TYPE=ciamar] -> "ciamar"
ARG[TYPE=cait] -> 'càit'
ARG[TYPE=co]  -> 'cò'
ARG[TYPE=de] -> 'dè'

AREL[+POL] -> "a"
AREL[-POL] -> "nach"

# POLAR ANSWERS WITH THE VERB, ANY VALENCY
S[INTERROG=?i, POL=?p] -> VI[INTERROG=?i, POL=?p, SUBCAT=?s]

### CONTINUOUS CONSTRUCTION WITH VERBAL NOUN - COMPLEMENTS OF THE VN ARE IN THE VNP IN THE VERBS_GRAM FILE
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT=subs, DEP=?d] NP[CASE=rad] VNP

### SENTENCES WITH UNIVALENT VERBS
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP", DEP=?d] NP[CASE=rad]

#[TOWRITE] DESCRIBE WHAT DEP=?D DOES AND WHY IT'S THERE? - GOOD QUESTION... WHY DID I PUT IT ON THE VI?
#THA MI GU MATH, THA THU AIR A' BHÒRD
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT=subs, DEP=?d] NP[CASE=rad] ARG[TYPE=ap]
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT=subs, DEP=?d] NP[CASE=rad] ARG[TYPE=pp]

#[Q] HOW TO INCLUDE "TRUE" ADVERBS AND HOW TO LINK THEM?
#S[INTERROG=?i, POL=?p] -> VI[INTERROG=?i, POL=?p, SUBCAT=subs] NP[CASE=rad] ADV

### SENTENCES WITH BIVALENT VERBS
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP:PP", PREP=?pp, DEP=?d] NP[CASE=rad] ARG[TYPE=pp, PREP=?pp]
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP:NP", DEP=?d] NP[CASE=rad] ARG[TYPE=np]
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP:PP|S", DEP=?d] NP[CASE=rad] ARG[TYPE=pp] | VI[INTERROG=?i, POL=?p, SUBCAT="NP:PP|S", DEP=?d] NP[CASE=rad] ARG[TYPE=clause]
S[INTERROG=?i, POL=?p, DEP=?d] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP:NP|S", DEP=?d] NP[CASE=rad] ARG[TYPE=np] | VI[INTERROG=?i, POL=?p, SUBCAT="NP:NP|S", DEP=?d] NP[CASE=rad] ARG[TYPE=clause]

### SENTENCES WITH TRIVALENT VERBS
S[INTERROG=?i, POL=?p] -> VI[INTERROG=?i, POL=?p, SUBCAT="NP:NP:PP"] NP[CASE=rad] ARG[TYPE=np] ARG[TYPE=pp]

### ARGUMENTS
ARG[TYPE=ap] -> AP | REL[TYPE=adj]
ARG[TYPE=pp, PREP=?pp] -> PP[PREP=?pp]
ARG[TYPE=np] -> NP[CASE=rad]

#[BIG-Q]? HOW TO TAKE ONLY DEPENDENT SENTENCE AS ARGUMENTS? THIS WOULD PROBABLY HAVE TO GO ALL THE WAY DOWN TO THE LEXICAL PRODUCTIONS OF VERBS, BECAUSE PASSING +DEF FROM VIs CONTAINING GU/NACH DOES NOT WORK
ARG[TYPE=clause] -> S[+DEP]
# ##########
# PRODUCTIONS FOR VERBS
# ##########

DO -> "do"
QAN[CAGR=neu] -> "an"
QAN[CAGR=lab] -> "am"
QAN[CAGR=bheil] -> "a"

CHA[CAGR=neu] -> "cha"
CHA[CAGR=v] -> "chan"

NACH -> "nach"

VI[TENSE=?t, SUBCAT=?s, +POL, +INTERROG] -> QAN[CAGR=?c] VD[CAGR=?c, TENSE=?t, SUBCAT=?s] | QAN[CAGR=?c] IRPAST[CAGR=?c, SUBCAT=?s]
VI[TENSE=?t, SUBCAT=?s, -POL, -INTERROG] -> CHA[CAGR=?c] VD[CAGR=?c, TENSE=?t, SUBCAT=?s] | CHA[CAGR=?c] IRPAST[CAGR=?c, SUBCAT=?s]

#THIS CANNOT BE +INTERROG BECAUSE OF CLAUSES WITH COMPLEMENTISER - UNDERSPECIFIED INTERROG HANDLES BOTH CASES WELL
VI[TENSE=?t, SUBCAT=?s, -POL, +DEP] -> NACH VD[-LEN, TENSE=?t, SUBCAT=?s]

#THIS IS THE +POL EQUIVALENT OF THE ABOVE
VI[TENSE=?t, SUBCAT=?s, +POL, -INTERROG, +DEP] -> COMP[CAGR=?c] VD[CAGR=?c, -LEN, TENSE=?t, SUBCAT=?s]


#+POL CLAUSES WITH COMPLEMENTISER
COMP[CAGR=lab] -> "gum"
COMP[CAGR=cop] -> "gur"
COMP[CAGR=neu] -> "gun"
COMP[CAGR=bheil] -> "gu"

#AIG WITH VNs, DIFFERENT FEATURES THAN THE PP AIG
AG[CAGR=v, -LEN] -> "ag" 
AG[CAGR=neu, -LEN] -> "a'"
AG[CAGR=lab, -LEN] -> "a'"

#PRONOUN OBJECTS OF VNs
GA[CAGR=neu, +LEN] -> "gam" | "gad" | "ga"
GA[CAGR=neu, -LEN] -> "ga" | "gan" | "gar" | "gur"
GA[CAGR=lab, -LEN] -> "gam"
GA[CAGR=v, -LEN] -> "ga_h-" | "gar_n-" | "gur_n-"

AIR -> "air"

#STANDARD VERBAL NOUN CONSTRUCTION WITH COMPLEMENTS
VNP[SUBCAT=?s] -> AG[CAGR=?c, LEN=?l] VN[CAGR=?c, SUBCAT=?s, LEN=?l]

#OTHER VERBAL NOUN CONSTRUCTIONS
VNP[SUBCAT=?s] -> GA[CAGR=?c, LEN=?l] VN[CAGR=?c, SUBCAT=?s, LEN=?l]
VNP[SUBCAT=?s] -> AIR VN[CAGR=?c, SUBCAT=?s]


#PAST DEPENDENT, WITH RAISING ALL IMPLEMENTED VALENCY STRINGS
#e.g. VD[TENSE=past, SUBCAT="NP:NP|PP"] -> DO[] VI[CAGR=?c, TENSE=past, SUBCAT="NP:NP|PP"] | IRPAST[SUBCAT="NP:NP|PP"]
#DISREGARDING CAGR FOR IRREGULARS - certain exceptions will arise, e.g. for FACA

VD[CAGR=neu, TENSE=past, SUBCAT=?s] -> DO[] VI[TENSE=past, SUBCAT=?s] | IRPAST[SUBCAT=?s]